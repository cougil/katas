// http://www.contextfreeart.org/gallery/view.php?id=3433
startshape go1
CF::Background = [b -1]

shape go1 {
  go [hue 140 sat 1 b 0.25]
}

shape go {
    goleft []
    goleft [r 180]
    goright []
}

shape goleft
rule 20 {
    mark []
    goleft [y 1 r 1 s 0.992 b 0.02 h 0.1]
}
rule 15 {
    mark []
    goleft [y 1 r 2 s 0.992 b 0.04 h 0.1]
}
rule 10 {
    mark []
    goleft [y 1 r 3 s 0.992 b -0.03 h 0.1]
}
rule 1 {
    mark []
    goleft [y 1 r 4 s 0.992 h -4]
    goright [y 1 r -4 s 0.992]
}

shape goright
rule 20 {
    mark []
    goright [y 1 r -1 s 0.992 b -0.02 h -0.1]
}
rule 15 {
    mark []
    goright [y 1 r -2 s 0.992 b -0.04 h -0.1]
}
rule 10 {
    mark []
    goright [y 1 r -3 s 0.992 b 0.03 h -0.1]
}
rule 1 {
    mark []
    goright [y 1 r -4 s 0.992]
    goleft [y 1 r 4 s 0.992 h 4]
}

shape mark {
    CIRCLE [s 10 hue 110 alpha -0.98]
    SQUARE [s 1]
}