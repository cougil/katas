// http://www.contextfreeart.org/gallery/view.php?id=2967
CF::Background = [hue 50 b -0.15 saturation 0.12]
// for some reason it doesn't draw anything with original view width/height
CF::Size = [s 3000 1600 x 300 y -100] // center on (-xpos, -ypos)
CF::MaxNatural = 100000

startshape STARTER
shape STARTER {
    loop i = 360 [r 1 x 10] {
        runB(4500,0.3,1.0,0.01,5.2,0.01,1) [z +1 r -90 y (rand(0,10)) x 1620]
        runB(4500,0.3,1.0,0.01,5.2,0.01,1) [z +1 r -90 y (rand(0,10)) x 1780]
        runB(4500,0.3,1.0,0.01,5.2,0.01,1) [z +1 r -90 y (rand(0,10)) x 1790]
        runB(4500,0.3,2.0,0.01,5.2,0.01,1) [z +1 r -90 y (rand(0,10))]
        loop 6 [r 1] runB(2200,0.3,2.0,0.01,0.06,-1,1) [z +4 r 180  y (rand(0,10))]
        runB(485,0.3,2.0,0.0,0.0,-1,1) [z +3 r 90 x 0.0 y 0.5 s 1 3 hue 50 b +1 saturation -1]
    }
    TREE [y -1200 x -1300 s 550 z +10000000000]
}

shape runB(natural length, number speedMin, number speedMax, number rotMin, number rotMax, number flipProb, natural depth)
rule {
    point[]
    if( depth < length ) {
        if ( flipProb > 0 && flipProb < ( rand(flipProb) + 0.001 ) ) {
            runB ( length, speedMin, speedMax, rotMin, rotMax, flipProb, depth ) [ flip 0 ]	}
        else {
            runB ( length, speedMin, speedMax, rotMin, rotMax, flipProb, depth +1 ) [ r rand(rotMin,rotMax)	x rand(speedMin,speedMax) ]	}
    }
}

shape point
rule { CIRCLE [ s rand(2, 5) a -rand(0.1, 0.9) y rand(-0.2, 0.2) ] }
rule { SQUARE [ r rand(0,360) s rand(2, 5) a -rand(0.1, 0.9) y rand(-0.2, 0.2) ] }

shape TREE {
    BRANCH (1,2) []
}

shape BRANCH ( natural depth, natural fl )
rule 98 {
    BRANCHPOINT (fl,depth) [ s 0.2 ]
    if( depth < 320 )	{
        BRANCH((depth +1),fl) [ y 0.1 s 0.985 r (rand(-1.0,4)*(sqrt(sqrt(depth))/1)) ] }
}
rule 5 {
    BRANCHPOINT (fl,depth) [ s 0.2 ]
    BRANCH((depth +1),(fl+1)) [ y 0.1 s 0.985 f 90 r rand(-5,-20) ]
    BRANCH((depth +1),fl) [ y 0.1 s 0.8 r 5 z +(100000*depth) ]
}

shape BRANCHPOINT(natural fl, natural depth) {
    if (depth < 40)
    {
        loop 6 [ x 1 ] runB(850,0.3,1.0,0.01,5.2,0.01,1) [ z -1000000000 r -90 s 0.0123 x -3 ]
    }

    CIRCLE [ x 0.0 b +0.30 h 0 sat +1 s 1.0 z +1 ]
    loop 3 [ y 0.1 ] {
        if( fl/2 == floor( (fl/2) ) )	{
            runB(4,0.1,1.0,0.01,0.06,-1,1) [ x -0.3 r 0 y -0.5 s 0.13 z +1.01 b -1 sat -1 a -0.5 ]
            runB(4,0.1,1.0,0.01,0.06,-1,1) [ x +0.1 r 0 y -0.5 s 0.13 z +1.015 b +1 sat -1 a -0.9 ]
        }
        else {
            runB(4,0.1,1.0,0.01,0.06,-1,1) [ x -0.0 r 0 y -0.5 s 0.13 z +1.01 b -1 sat -1 a -0.5 ]
            runB(4,0.1,1.0,0.01,0.06,-1,1) [ x -0.3 r 0 y -0.5 s 0.13 z +1.015 b +1 sat -1 a -0.9 ]
        }
    }
    runB( 20,0.1,1.0,0.01,0.06,-1,1 ) [ x -0.5 r 90 y -0.0 s 0.04 z +1.16 b -1 sat -1 a -0.09 ]
    runB( 20,0.1,1.0,0.01,0.06,-1,1 ) [ x 0.5 r 90 y -0.0 s 0.04 z +1.16 b -1 sat -1 a -0.09 ]
}
