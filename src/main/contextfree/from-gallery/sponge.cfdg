// Menger sponge and friends (http://en.wikipedia.org/wiki/Menger_sponge). By AK May 2009
// Uses similar principles to Crystals (Gallery id 1447) and other designs, but on a three-fold basis.
startshape mengerSponge
CF::Background = [b -0.8 hue 270 sat 0.5]

shape mengerSponge {
    loop 3 [r 120] {
        face [b 0.5 sat 1 hue 15]
    }
}

shape face {
    // should be something like this:
    SQUARE [skew 0 -30 s sqrt(0.75) 1 x -0.434 y -0.2  a -0.3]
    // old code, broken in v3: SQUARE [skew 0 -30 s sqrt(0.75) 1 x -0.5 y -0.5  a -0.3]

    facelet [z (4/6) b 0.2]
    facelet [y (-1/3) z (-1/6) z (3/6) b 0.2]
    facelet [y (-2/3) z (-2/6) z (2/6) b 0.1]

    facelet [x (-sqrt(0.75)/3) y (1/6) z (3/6) b 0.2]
//	facelet [x (-sqrt(0.75)/3) y (-1/6) z (2/6)]
    facelet [x (-sqrt(0.75)/3) y (-3/6) z (1/6) b -0.1]

    facelet [x (-2*sqrt(0.75)/3) y (2/6) z (2/6) b 0.1]
    facelet [x (-2*sqrt(0.75)/3) y (0/6) z (1/6) b -0.1]
    facelet [x (-2*sqrt(0.75)/3) y (-2/6) b -.2]

    facelet [r -120 x (-sqrt(0.75)/3) y (-3/6) z (1/6) b -0.5]
    facelet [r 120 x (-2*sqrt(0.75)/3) z (1/6) b -0.5]

    loop 6 [flip 0 h -20 s 0.95 0.95 0.95] {
        whisker [z (1/3) r -150 h -120 b -1 sat -0.5]
    }
}

shape facelet
rule {
    face [s (1/3) (1/3) (1/3)]
}
rule 0.5 {
    facelet [b -0.05]
}
rule 0.5 {
    facelet [b 0.05]
}


shape whisker
rule {
    CIRCLE [s 0.05]
    whisker [x 0.015 z 0.005 s 0.99 0.99 0.99 r 2 b 0.001 sat 0.001]
}
rule 0.1 {
    whisker [flip 0]
}
//rule whisker 0.001 {}